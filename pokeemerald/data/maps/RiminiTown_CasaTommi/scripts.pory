raw `
.set LOCALID_TOMMI, 1
`

mapscripts RiminiTown_CasaTommi_MapScripts {
	
}

script RiminiTown_CasaTommi_EventScript_Tommi {
	lockall
	applymovement(LOCALID_TOMMI, Common_Movement_FacePlayer)
	waitmovement(0)

	if(flag(FLAG_TALKED_WITH_TOMMI)== false) {

		msgbox (format("HO LA MOSSA, GAS.\p 
Tu oggi ti laurei, vero? Se mi porti i tuoi Pokémon, 
posso insegnargli tutte le mosse che imparano con l'esperienza.\p
Quelle con le MT e le MN, invece, tocca che le compri.
Sicuramente a BOLOGNA le hanno."))

		delay(40)
		applymovement(LOCALID_TOMMI, Common_Movement_ExclamationMark)
		waitmovement(0)
		msgbox(format("Vuoi sapere cosa c'è dentro il covo
di Gnassi?!\p
Bella domanda, sicuro qualcosa di potente.
C'è chi dice nasconda dei Pokémon estremamente rari.
Ma se vuoi sapere qualcosa di più preciso, ti consiglio
di chiedere a Squadra."))

		setflag(FLAG_TALKED_WITH_TOMMI)
	
	}

	call(RiminiTown_CasaTommi_EventScript_InsegnaMosse)

	msgbox(format("Torna quando hai bisogno di una mossa."))
	releaseall
}

script RiminiTown_CasaTommi_EventScript_InsegnaMosse {
	
	msgbox(format("Vuoi che insegni una mossa a uno dei tuoi Pokémon?"), MSGBOX_YESNO)

	if (var(VAR_RESULT)==NO) {
		return
	}

	if (flag(FLAG_SYS_POKEMON_GET)==false) {
		msgbox(format("Peccato che non hai un cazzo di Pokémon, Gassi."))
		return
	}

	msgbox(format("A quale Pokémon vuoi insegnarla?"))
	special(ChooseMonForMoveRelearner)
	waitstate
	
	if(var(VAR_TEMP_PAR_4)==PARTY_NOTHING_CHOSEN) {
		return
	}

	special(IsSelectedMonEgg)

	if(var(VAR_RESULT)==TRUE){
		msgbox(format("Mi pigli per il culo Gassi?
Con un uovo se vuoi ci faccio la frittata."))
		return
	}

	if(var(VAR_TEMP_PAR_5)==0) {
		msgbox(format("Mmh, non ho niente da insegnare al ragazzo, 
è ancora troppo giovane."))
		return
	}
		
	msgbox(format("Quale mossa vuoi che impari?"))
	special(TeachMoveRelearnerMove)
	waitstate

	return
}