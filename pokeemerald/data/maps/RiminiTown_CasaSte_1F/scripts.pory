raw `
.set STE_ID, 2

RiminiTown_CasaSte_1F_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, RiminiTown_CasaSte_1F_OnTransition
	map_script MAP_SCRIPT_ON_FRAME_TABLE, RiminiTown_CasaSte_1F_OnFrame
	.byte 0

RiminiTown_CasaSte_1F_OnTransition:
	call_if_eq VAR_RIMINI_INTRO_STATE, 3, RiminiTown_CasaSte_1F_EventScript_PlaceSte
	end

RiminiTown_CasaSte_1F_OnFrame:
	map_script_2 VAR_RIMINI_INTRO_STATE, 3, RiminiTown_CasaSte_1F_EventScript_PassaSte
	.2byte 0

`

script RiminiTown_CasaSte_1F_EventScript_MammadiSte {

	lockall
	faceplayer

	if(flag(FLAG_CONSEGNATO_CAFFE)) {
		msgbox(format("Grazie per il caffé!"))
	}
	else {
		msgbox(format("Buongiorno! Posso aiutarti?"))
		delay(40)
		msgbox(format("Oh, il caffé che avevo ordinato!\p
Grazie, tu allora devi essere Gas, Stefano mi ha parlato di te.
Non credo che sia in casa ora, probabilmente sarà
alla cava a bighellonare."))
		removeitem(ITEM_CAFFE)
		setflag(FLAG_CONSEGNATO_CAFFE)
	}

	releaseall
	end
}

script RiminiTown_CasaSte_1F_EventScript_PlaceSte {
	setobjectxyperm(STE_ID, 2, 8)
	setobjectmovementtype(STE_ID, MOVEMENT_TYPE_FACE_UP)	
}

script RiminiTown_CasaSte_1F_EventScript_PassaSte {
	lockall
	msgbox(format("STE: Ciao Mamma!"))
	closemessage
	applymovement(STE_ID, RiminiTown_CasaSte_1F_Movement_SteVaSu)
	delay(20)
	applymovement(OBJ_EVENT_ID_PLAYER, RiminiTown_CasaSte_1F_Movement_GasVaSu)
	waitmovement(0)
	setvar(VAR_RIMINI_INTRO_STATE, 4)
	hideobjectat(OBJ_EVENT_ID_PLAYER, 0)
	warpsilent(MAP_RIMINI_TOWN_CASA_STE_2F, 1, 2)
	waitstate
	releaseall
	end
}

movement RiminiTown_CasaSte_1F_Movement_SteVaSu {
	walk_up*6
	set_invisible
}

movement RiminiTown_CasaSte_1F_Movement_GasVaSu {
	walk_right
	walk_up*6
}