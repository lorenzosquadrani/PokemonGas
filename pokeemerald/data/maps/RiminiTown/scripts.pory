raw `
.set LOCALID_STE, 1
.set LOCALID_MOM, 4
.set LOCALID_RIVAL, 7
.set LOCALID_BIRCH, 8

RiminiTown_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, RiminiTown_OnTransition
	map_script MAP_SCRIPT_ON_FRAME_TABLE, RiminiTown_OnFrame
	.byte 0

RiminiTown_OnTransition:
	setflag FLAG_VISITED_RIMINI_TOWN
	end

RiminiTown_OnFrame:
	map_script_2 VAR_RIMINI_INTRO_STATE, 1, RiminiTown_EventScript_StepOffTruck
	.2byte 0

RiminiTown_EventScript_StepOffTruck::
	lockall
	setvar VAR_TEMP_PAR_4, 5
	setvar VAR_TEMP_PAR_5, 8
	call RiminiTown_EventScript_GoInsideWithMom
	warpsilent MAP_RIMINI_TOWN_CASA_GAS_1F, 8, 8
	waitstate
	releaseall
	end

RiminiTown_EventScript_HideMapNamePopup::
	setflag FLAG_HIDE_MAP_NAME_POPUP
	return

RiminiTown_Movement_MomExitHouse:
	walk_down
	step_end

RiminiTown_Movement_MomApproachDoor:
	delay_16
	delay_8
	walk_in_place_faster_up
	step_end

RiminiTown_Movement_PlayerApproachDoor:
	delay_16
	delay_8
	walk_right
	walk_up
	step_end

RiminiTown_Movement_MomEnterHouse:
	walk_up
	set_invisible
	step_end

RiminiTown_Movement_PlayerEnterHouse:
	walk_up
	walk_up
	step_end

RiminiTown_Movement_PlayerStepOffTruck:
	jump_right
	delay_16
	delay_16
	delay_16
	step_end
`

script RiminiTown_EventScript_GoInsideWithMom{
	delay(15)
	playse(SE_LEDGE)
	applymovement(OBJ_EVENT_ID_PLAYER, RiminiTown_Movement_PlayerStepOffTruck)
	waitmovement(0)
	opendoor(11, 20)
	waitdooranim
	addobject(LOCALID_MOM)
	applymovement(LOCALID_MOM, RiminiTown_Movement_MomExitHouse)
	waitmovement(0)
	closedoor(11,20)
	waitdooranim
	delay(10)
	msgbox(RiminiTown_Text_ANDREAGASPARI, MSGBOX_DEFAULT)
	closemessage
	applymovement(LOCALID_MOM, RiminiTown_Movement_MomApproachDoor)
	applymovement(OBJ_EVENT_ID_PLAYER, RiminiTown_Movement_PlayerApproachDoor)
	waitmovement(0)
	opendoor(11, 20)
	waitdooranim
	applymovement(LOCALID_MOM, RiminiTown_Movement_MomEnterHouse)
	applymovement(OBJ_EVENT_ID_PLAYER, RiminiTown_Movement_PlayerEnterHouse)
	waitmovement(0)
	setflag(FLAG_HIDE_RIMINI_TOWN_MOM_OUTSIDE)
	hideobjectat(OBJ_EVENT_ID_PLAYER, 0)
	closedoor(11, 20)
	waitdooranim
	clearflag(FLAG_HIDE_MAP_NAME_POPUP)
	return
}

script RiminiTown_EventScript_SteSign {
	msgbox("Casa di Ste", MSGBOX_SIGN)
	end
}

script RiminiTown_EventScript_CavaSign {
	msgbox("La Cava", MSGBOX_SIGN)
	end
}

script RiminiTown_EventScript_GasSign {
	msgbox("Casa di Gas", MSGBOX_SIGN)
	end
}

script RiminiTown_EventScript_TommiSign {
	msgbox("Casa di Tommi", MSGBOX_SIGN)
	end
}

script RiminiTown_EventScript_GnassiSign {
	msgbox("Covo di Gnassi", MSGBOX_SIGN)
	end
}

script RiminiTown_EventScript_TownSign {
	msgbox("RIMINI TOWN", MSGBOX_SIGN)
	end
}

text RiminiTown_Text_ANDREAGASPARI {
	format("ANDREA GASPARI!\p
Non voglio neanche sapere cos'hai fatto stanotte 
e perché sei tornato con quel furgone. 
Ti ricordo, qual'ora fossi ancora ubriaco, che oggi pomeriggio
ti LAUREI.\p
Forza, vieni a salutare il babbo prima di andare a Bologna.")
}

script RiminiTown_EventScript_IncontroConSte {

	msgbox(format("STE: Bella Gas!
Oggi pomeriggio ti laurei vero? Daje!\p
Mi è appena arrivato un pacco di DROGA."))

	msgbox(format("Che dici, ci facciamo una canna?"), MSGBOX_YESNO)

	if (var(VAR_RESULT)!=1) {
		msgbox(format("Mmh, sicuro sicuro?"), MSGBOX_YESNO)

		while (var(VAR_RESULT)!=1) {
			msgbox(format("Dai"), MSGBOX_YESNO)
		}
	}

	msgbox(format("Bravo! Vieni, andiamo a casa mia a prendere la roba."))
	closemessage
	setvar(VAR_RIMINI_INTRO_STATE, 3)
	applymovement(LOCALID_STE, RiminiTown_Movement_SteVersoCasa)
	delay(40)
	applymovement(OBJ_EVENT_ID_PLAYER, RiminiTown_Movement_GasVersoCasaSte)
	waitmovement(0)
	opendoor(21, 20)
	waitdooranim
	applymovement(LOCALID_STE, RiminiTown_Movement_SteEntra)
	applymovement(OBJ_EVENT_ID_PLAYER, RiminiTown_Movement_GasEntraSte)
	waitmovement(0)
	hideobjectat(OBJ_EVENT_ID_PLAYER, 0)
	closedoor(21, 20)
	waitdooranim
	setflag(FLAG_INCONTRATO_STE)
	clearflag(FLAG_HIDE_STE_1F)
	clearflag(FLAG_HIDE_STE_2F)
	// Scenetta della gente davanti a casa di ste
	// Ma non so come far spawnare la gente
	//waitmovement(LOCALID_STE)
	//msgbox(format("Oh no, ci risiamo!"))
	//waitmovement(OBJ_EVENT_ID_PLAYER)
	//msgbox(format("ANDATE VIA SUBITO, O CHIAMO LA POLIZIA!"))
	//closemessage
	warpsilent(MAP_RIMINI_TOWN_CASA_STE_1F, 1, 8)
	waitstate
	releaseall
	end
}

script RiminiTown_EventScript_NeedPokemonTriggerDown{
	lockall
	msgbox(format("SEI MATTO!? È pericoloso andare fuori città senza Pokemons."))
	applymovement(OBJ_EVENT_ID_PLAYER, RiminiTown_Movement_GasMoveDown )
	waitmovement(0)
	releaseall
	end
}

script RiminiTown_EventScript_NeedPokemonTriggerUp{
	lockall
	msgbox(format("SEI MATTO!? È pericoloso andare fuori città senza Pokemons."))
	applymovement(OBJ_EVENT_ID_PLAYER, RiminiTown_Movement_GasMoveUp )
	waitmovement(0)
	releaseall
	end
}

script RiminiTown_EventScript_GasVersoSteTrigger1{
	lockall
	applymovement(OBJ_EVENT_ID_PLAYER, RiminiTown_Movement_GasVersoSte1)
	waitmovement(0)
	call(RiminiTown_EventScript_IncontroConSte)
	end
}

script RiminiTown_EventScript_GasVersoSteTrigger2{
	lockall
	applymovement(OBJ_EVENT_ID_PLAYER, RiminiTown_Movement_GasVersoSte2)
	waitmovement(0)
	call(RiminiTown_EventScript_IncontroConSte)
}

script RiminiTown_EventScript_DoveCazzo {
	msgbox(format("Ma dove cazzo vai cojone.\p Non li trovi i bugs."))
	closemessage
}

movement RiminiTown_Movement_GasVersoSte1 {
	walk_up*7
}

movement RiminiTown_Movement_GasVersoSte2 {
	walk_up*6
	walk_right
	walk_up
}

movement RiminiTown_Movement_GasMoveUp {
	walk_up
}

movement RiminiTown_Movement_GasMoveDown {
	walk_down
}

movement RiminiTown_Movement_SteVersoCasa {
	walk_left
	walk_down*11
	walk_left*9
	walk_left*4
	walk_up
}

movement RiminiTown_Movement_GasVersoCasaSte {
	walk_left
	walk_down*10
	walk_left*8
	walk_left*5
	face_up
}

movement RiminiTown_Movement_SteEntra {
	walk_up
	set_invisible
}

movement RiminiTown_Movement_GasEntraSte {
	walk_up*2
}
